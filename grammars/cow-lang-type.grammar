//  author: samtenka
//  change: 2020-03-14
//  create: 2020-03-15
//  descrp:
//  to use:

MAIN = _ OUTER_PROGRAM _

//=============================================================================
//====  0. GUARDED COMMAND LANGUAGE  ==========================================
//=============================================================================

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  0.0. Categorical Basics  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

//--------------------  0.0.0. sequencing  ------------------------------------

OUTER_PROGRAM! = ( OUTER_STATEMENT _ ( ";" _ OUTER_PROGRAM | EMPTY ) | EMPTY )   
OUTER_STATEMENT! = TYPEDEFN | FUNCTION

INNER_PROGRAM! = ( INNER_STATEMENT ( _ ";" _ INNER_PROGRAM | EMPTY ) | EMPTY )   
INNER_STATEMENT! = SKIP | ABORT | IF | DO | MATCH | PRINT | DECLARATION | ASSIGNMENT

//--------------------  0.0.1. initial and terminal constructs  ---------------

ABORT = "abort"
SKIP = "skip"

DECLARATION = "var" _ JUDGEMENT
PRINT = "print" _ LOWER_IDENTIFIER

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  0.1. Guarded Composititions  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

EXPRESSION_GUARDED_PROGRAM = EXPR _ "~>" _ INNER_PROGRAM
IDENTIFIER_PAIR_GUARDED_PROGRAM = IDENTIFIER_PAIR _ "~>" _ INNER_PROGRAM
IDENTIFIER_PAIR = LOWER_IDENTIFIER _ LOWER_IDENTIFIER

//--------------------  0.1.0. alternative construct  -------------------------

IF = "if" _ "{" _ IFBODY _ "}"
IFBODY! = EXPRESSION_GUARDED_PROGRAM _ ( IFBODY | EMPTY )

//--------------------  0.1.1. repetitive construct  --------------------------

DO = "do" _ "{" _ DOBODY _ "}"
DOBODY! = EXPRESSION_GUARDED_PROGRAM _ ( DOBODY | EMPTY )

//--------------------  0.1.2. coproduct matching construct  ------------------

MATCH = "match" _  EXPR _ "{" _ MATCHBODY _ "}"
MATCHBODY! =  IDENTIFIER_PAIR_GUARDED_PROGRAM _ ( MATCHBODY | EMPTY )

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  0.2. Assignment  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
ASSIGNMENT = COORDINATE _ "=" _ EXPR

//=============================================================================
//====  1. OUTER DEFINITIONS  =================================================
//=============================================================================

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  1.0. Type Definition  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

TYPEDEFN = COMPKIND _ UPPER_IDENTIFIER _ "{" _  JUDGEMENTS _ "}"
COMPTYPE = COMPKIND _ "{"  _  JUDGEMENTS _ "}"
TYPE = BASETYPE | COMPTYPE 
BASETYPE = "Unit" | "Bool" | "Char" | "Int" | "Float" | UPPER_IDENTIFIER 
COMPKIND = "struct" | "enum" 
JUDGEMENTS! = JUDGEMENT _ ( ";" _ JUDGEMENTS | EMPTY )
JUDGEMENT = LOWER_IDENTIFIER _ ":" _ TYPE

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  1.1. Function Definition  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FUNCTION = "fn" _ FUNCTIONHEADER _ FUNCTIONBODY
FUNCTIONHEADER = LOWER_IDENTIFIER _ FUNCTIONTYPE
FUNCTIONTYPE = "(" _ FUNCTIONARGLIST _ ")" _ ":" _ TYPE
FUNCTIONBODY! = "{" _ INNER_PROGRAM _ "}"
FUNCTIONARGLIST! = JUDGEMENT _ ( "," _ FUNCTIONARGLIST | EMPTY ) | EMPTY

//=============================================================================
//====  2. EXPRESSIONS  =======================================================
//=============================================================================

EXPR = OR_EXPR

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.0. logic  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

OR_EXPR! = AND_EXPR _ ( "or" _ OR_EXPR | EMPTY )
AND_EXPR! = NOT_EXPR _ ( "and" _ AND_EXPR | EMPTY )
NOT_EXPR! = "not" _ NOT_EXPR | "true" | "false" | EQ_EXPR

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.1. comparisons  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

EQ_EXPR! = PLUS_EXPR _ ( ( "==" | "!=" | "<=" | "<" | ">=" | ">" ) _ EQ_EXPR | EMPTY )

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.2. arithmetic  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

PLUS_EXPR! = TIMES_EXPR _ ( ( "+" | "-" ) _ PLUS_EXPR | EMPTY )
TIMES_EXPR! = NEG_EXPR | NEG_EXPR _ ( ( "*" | "/" | "%" ) _ TIMES_EXPR | EMPTY )
NEG_EXPR! = "-" _ TIMES_EXPR | NUMBER | FACTOR 

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.3. leaves  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

FACTOR! = "(" _ EXPR _ ")" | COORDINATE ( "(" _ FUNCCALLARGS _ ")" | EMPTY )

FUNCCALLARGS! = EXPR _ ( "," _ FUNCCALLARGS | EMPTY ) | EMPTY

COORDINATE = GETCHAIN
GETCHAIN! = LOWER_IDENTIFIER ( "." GETCHAIN | EMPTY ) 

//=============================================================================
//====  3. LEXER  =============================================================
//=============================================================================

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.0. identifiers  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

UPPER_IDENTIFIER = UPPER UPPER_IDENTIFIERLOOP
LOWER_IDENTIFIER = LOWER LOWER_IDENTIFIERLOOP

UPPER_IDENTIFIERLOOP! = ( ( LOWER | UPPER     ) UPPER_IDENTIFIERLOOP | EMPTY )
LOWER_IDENTIFIERLOOP! = ( ( LOWER | "_" LOWER ) LOWER_IDENTIFIERLOOP | EMPTY )

UPPER* = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"
LOWER* = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" 

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.1. numbers  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

NUMBER = NUMBERLOOP ( "." NUMBERLOOP | EMPTY )
NUMBERLOOP! = DIGIT ( NUMBERLOOP | EMPTY )
DIGIT* = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
//~~~~~~~~~~~~  3.2. whitespace  ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

_* = WHITE
WHITE* = " " WHITE | EMPTY
EMPTY* = ""
